pkgname=aur
pkgver=1
pkgrel=1
arch=('i686' 'x86_64')
_py_deps=(bleach markdown mysqlclient pygit2 setuptools srcinfo poetry)
depends=(nginx mysql fcgiwrap openssh git cgit curl "${_py_deps[@]/#/python-}")
makedepends=(python-poetry-dynamic-versioning python-installer)
_aurweb_ver=6.2.18
source=(git+https://gitlab.archlinux.org/archlinux/aurweb.git#tag=v$_aurweb_ver
		nginx.conf
		aurweb-config
		php.ini
		php-fpm.conf
		sshd_config
		cgitrc
		aurweb-git-auth.sh
		aurweb-git-serve.sh
		start.sh
		setup.sh)
md5sums=('SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP')

_aur_uid=500
_aur_gid=500

package() {
  mkdir -p "$pkgdir"/opt/aur

  mkdir "$pkgdir"/opt/aur/aurweb
  git -C "$srcdir"/aurweb archive HEAD | tar -x -C "$pkgdir"/opt/aur/aurweb

  (
	printf 'g aur %d\n' "$_aur_gid"
	printf 'u aur %d "AUR user" /opt/aur /bin/sh\n' "$_aur_uid"
  ) | install -Dm644 /dev/stdin "$pkgdir"/usr/lib/sysusers.d/aur.conf
  printf 'd /opt/aur/run - aur aur -\n'  \
	| install -Dm644 /dev/stdin "$pkgdir"/usr/lib/tmpfiles.d/aur.conf

  # Use Poetry to build and install aurweb
  cd "$srcdir"/aurweb
  poetry build -f wheel
  /usr/bin/python -m installer --destdir="$pkgdir" dist/*.whl
  install -m644 -D "$srcdir"/aurweb-config "$pkgdir"/etc/aurweb/config

  # Install Poetry wrapper scripts for aurweb-git-auth and aurweb-git-serve
  # This is needed because aurweb v6.x uses Poetry virtualenv
  # The wheel installs Python entry points, but we override them with these wrappers
  install -m755 -D "$srcdir"/aurweb-git-auth.sh "$pkgdir"/usr/bin/aurweb-git-auth
  install -m755 -D "$srcdir"/aurweb-git-serve.sh "$pkgdir"/usr/bin/aurweb-git-serve

  install -m644 -D "$srcdir"/nginx.conf "$pkgdir"/opt/aur/nginx.conf

  install -m644 -D "$srcdir"/php.ini "$pkgdir"/opt/aur/php.ini
  install -m644 -D "$srcdir"/php-fpm.conf "$pkgdir"/opt/aur/php-fpm.conf

  mkdir -p "$pkgdir"/opt/aur/cgit/cache
  install -m644 -D "$srcdir"/cgitrc "$pkgdir"/opt/aur/cgitrc

  install -m644 -D "$srcdir"/sshd_config "$pkgdir"/opt/aur/sshd_config

  install -m755 -D "$srcdir"/start.sh "$pkgdir"/opt/aur/start.sh
  install -m755 -D "$srcdir"/setup.sh "$pkgdir"/opt/aur/setup.sh

  chown -R "$_aur_uid":"$_aur_gid" "$pkgdir"/opt/aur
}
